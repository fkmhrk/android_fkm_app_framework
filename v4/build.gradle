apply plugin: 'com.android.library'
apply plugin: 'maven'

def VERSION = '0.8.0'

uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://$System.env.HOME/.m2/repository"

        pom.version = VERSION
        pom.groupId = 'jp.fkmsoft.libs'
        pom.artifactId = 'android-app-framework-v4'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "20.0.0"
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }    
}

dependencies {
    compile 'com.android.support:support-v4:22.2.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
